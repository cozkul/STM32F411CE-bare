cmake_minimum_required(VERSION 3.15.3)

# Optional: print out extra messages to see what is going on. Comment it to have less verbose messages
set(CMAKE_VERBOSE_MAKEFILE ON)

# Path to toolchain file. This one has to be before 'project()' below
set(CMAKE_TOOLCHAIN_FILE ${CMAKE_SOURCE_DIR}/arm-none-eabi-gcc.cmake)

# Setup project, output and linker file
project(STM32F411CE-bare VERSION 1.0)
set(EXECUTABLE ${PROJECT_NAME}.elf)
set(LINKER_FILE ${CMAKE_SOURCE_DIR}/device/linker.ld)

enable_language(C ASM)
set(CMAKE_C_STANDARD 17)
set(CMAKE_C_STANDARD_REQUIRED ON)
set(CMAKE_C_EXTENSIONS OFF)
set(CMAKE_CXX_STANDARD 20)
set(CMAKE_CXX_STANDARD_REQUIRED ON)
set(CMAKE_CXX_EXTENSIONS OFF)

# Optional: issue a message to be sure it uses the correct toolchain file.
message(STATUS "CMAKE_TOOLCHAIN_FILE is: ${CMAKE_TOOLCHAIN_FILE}")

# List of source files
set(SRC_FILES
        device/startup.S
        device/system.c
        src/main.c
        )

# Build the executable based on the source files
add_executable(${EXECUTABLE} ${SRC_FILES})

set(TARGET_FLAGS
# No semihosting: printf, keyboard input thru host computer
        --specs=nosys.specs
        -mcpu=cortex-m4
        -mthumb
        -mlittle-endian
        -mfpu=fpv4-sp-d16
        -mfloat-abi=hard
        )

# List of compiler defines, prefix with -D compiler option
target_compile_definitions(${EXECUTABLE} PRIVATE
        -DSTM32F411xE
        -DHSE_VALUE=25000000 # https://docs.zephyrproject.org/2.6.0/boards/arm/blackpill_f411ce/doc/index.html
        )

# List of include directories
target_include_directories(${EXECUTABLE} PRIVATE
        src
        device
        )

# Compiler options
# Right flags on documentation of processor
target_compile_options(${EXECUTABLE} PRIVATE
        ${TARGET_FLAGS}

        -fdata-sections         # data will be at different sections in the .elf file
        -ffunction-sections     # functions will be at different sections
        # cleaning up unused sections can be done easily

        -Wall
        -Wextra
        -Wpedantic
        -Werror
        -O1
        -g3
        )

# Linker options
target_link_options(${EXECUTABLE} PRIVATE
        -Wl,-T${LINKER_FILE}

        ${TARGET_FLAGS}

        --specs=nano.specs
        -lc
        -lm
        -lnosys
        -Wl,-Map=${PROJECT_NAME}.map,--cref
        -Wl,--gc-sections
        -Xlinker -print-memory-usage -Xlinker
        )

# Optional: Print executable size as part of the post build process
add_custom_command(TARGET ${EXECUTABLE}
        POST_BUILD
        COMMAND ${CMAKE_SIZE_UTIL} ${EXECUTABLE})

# Optional: Create hex, bin and S-Record files after the build
add_custom_command(TARGET ${EXECUTABLE}
        POST_BUILD
        COMMAND ${CMAKE_OBJCOPY} -O srec --srec-len=64 ${EXECUTABLE} ${PROJECT_NAME}.s19
        COMMAND ${CMAKE_OBJCOPY} -O ihex ${EXECUTABLE} ${PROJECT_NAME}.hex
        COMMAND ${CMAKE_OBJCOPY} -O binary ${EXECUTABLE} ${PROJECT_NAME}.bin)